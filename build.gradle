description = "Post on my fridge"

apply plugin: 'java'
defaultTasks 'clean','gaeDownloadSdk', 'gaeExplodeWar', 'minifyFridgeCss','minifyFridgeJs','gaeRun'

repositories {
	mavenCentral()
	mavenRepo name:'objectify-appengine-releases', url:'http://maven.objectify-appengine.googlecode.com/git/'
}

buildscript {
	repositories { mavenCentral() }
	dependencies {
		classpath 'com.eriwen:gradle-js-plugin:1.1'
		classpath 'com.eriwen:gradle-css-plugin:1.1.2'
	}
}

apply plugin: 'js'

task combineFridgeJs(type: com.eriwen.gradle.js.tasks.CombineJsTask) {
	source = [
		"${projectDir}/src/main/webapp/js/util/tools.js",
		"${projectDir}/src/main/webapp/js/app.js",
		"${projectDir}/src/main/webapp/js/util/jquery-ui-ember.js",
		"${projectDir}/src/main/webapp/js/controller/headerController.js",
		"${projectDir}/src/main/webapp/js/view/headerView.js",
		"${projectDir}/src/main/webapp/js/model/postModel.js",
		"${projectDir}/src/main/webapp/js/view/postView.js",
		"${projectDir}/src/main/webapp/js/controller/fridgeController.js",
		"${projectDir}/src/main/webapp/js/view/fridgeView.js",
		"${projectDir}/src/main/webapp/js/model/messageModel.js",
		"${projectDir}/src/main/webapp/js/view/messageView.js",
		"${projectDir}/src/main/webapp/js/view/chatView.js",
		"${projectDir}/src/main/webapp/js/controller/chatController.js"
	]
	dest = file("${buildDir}/exploded-war/js/fridge.js")
}

task minifyFridgeJs(type: com.eriwen.gradle.js.tasks.MinifyJsTask, dependsOn: 'combineFridgeJs') {
	source = combineFridgeJs
	dest = file("${buildDir}/exploded-war/js/fridge.min.js")
	closure {
		closure.compilationLevel = 'SIMPLE_OPTIMIZATIONS'
	}
}

apply plugin: 'css'

css.source {
    app {
        css {
            srcDir "${projectDir}/src/main/webapp/css/"
            include "*.css"
        }
    }
}

task combineFridgeCss(type: com.eriwen.gradle.css.tasks.CombineCssTask) {
	source = css.source.app.css.files
	dest = file("${buildDir}/exploded-war/css/fridge.css")
}

task minifyFridgeCss(type: com.eriwen.gradle.css.tasks.MinifyCssTask, dependsOn: 'combineFridgeCss') {
	source = combineFridgeCss
	dest = file("${buildDir}/exploded-war/css/fridge.min.css")
}

buildscript {
	repositories {
		add(new org.apache.ivy.plugins.resolver.URLResolver()) {
			name = 'GitHub'
			addArtifactPattern 'http://cloud.github.com/downloads/[organisation]/[module]/[module]-[revision].[ext]'
		}
	}
	dependencies { classpath 'bmuschko:gradle-gae-plugin:0.7.6' }
}

apply plugin: 'gae'

gae {
	httpPort = 8888
	stopKey = null
	disableUpdateCheck = true
	downloadSdk = true
	appcfg { email = 'arnaud.gourlay@gmail.com' }
	jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000']
}

dependencies {
	gaeSdk 'com.google.appengine:appengine-java-sdk:1.7.3'
	compile 'com.google.appengine:appengine-api-1.0-sdk:1.7.3'
	compile 'javax.servlet:servlet-api:2.5'
	compile 'joda-time:joda-time:2.1'
	compile 'com.sun.jersey:jersey-bundle:1.15'
	compile 'com.google.guava:guava:13.0.1'
	compile 'org.codehaus.jackson:jackson-core-asl:1.9.9'
	compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.9'
	compile 'org.codehaus.jackson:jackson-jaxrs:1.9.9'
	compile 'org.codehaus.jackson:jackson-xc:1.9.9'
	compile 'com.googlecode.objectify:objectify:3.1'
	compile 'javax.persistence:persistence-api:1.0.2'
	compile 'asm:asm:3.3.1'
	testCompile 'junit:junit:4.10'
}