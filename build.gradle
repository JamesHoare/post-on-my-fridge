description = "Post on my fridge"

apply plugin: 'java'
defaultTasks 'clean','gaeDownloadSdk', 'gaeExplodeWar', 'minifyFridgeCss','jshintFridge','csslint','minifyFridgeJs','combineLibJs','combineLibJqueryDependent', 'gaeRun'

repositories {
	mavenCentral()
	mavenRepo name:'objectify-appengine-releases', url:'http://maven.objectify-appengine.googlecode.com/git/'
}

buildscript {
	repositories { mavenCentral() }
	dependencies {
		classpath 'com.eriwen:gradle-js-plugin:1.1'
		classpath 'com.eriwen:gradle-css-plugin:1.1'
	}
}

apply plugin: 'js'

task jshintFridge(type: com.eriwen.gradle.js.tasks.JsHintTask) {
	source = [
		"${projectDir}/src/main/webapp/js/app/util/tools.js",
		"${projectDir}/src/main/webapp/js/app/app.js",
		"${projectDir}/src/main/webapp/js/app/util/jquery-ui-ember.js",
		"${projectDir}/src/main/webapp/js/app/model/postModel.js",
		"${projectDir}/src/main/webapp/js/app/view/postView.js",
		"${projectDir}/src/main/webapp/js/app/controller/fridgeController.js"
	]
	dest = file("${buildDir}/jshint.out")
}

task combineLibJs(type: com.eriwen.gradle.js.tasks.CombineJsTask) {
	source = [
		"${projectDir}/src/main/webapp/js/lib/underscore-1.3.3.min.js",
		"${projectDir}/src/main/webapp/js/lib/moment-1.7.0.min.js",
		"${projectDir}/src/main/webapp/js/lib/handlebars-1.0.0.beta.6.min.js",
		"${projectDir}/src/main/webapp/js/lib/humane-3.0.4.min.js",
		"${projectDir}/src/main/webapp/js/lib/purl-2.2.1.min.js",
		"${projectDir}/src/main/webapp/js/lib/spin-1.2.6.min.js"

	]
	dest = file("${buildDir}/exploded-war/js/app/lib.min.js")
}

task combineLibJqueryDependent(type: com.eriwen.gradle.js.tasks.CombineJsTask) {
	source = [
		"${projectDir}/src/main/webapp/js/lib/ember-1.0.pre.min.js",
		"${projectDir}/src/main/webapp/js/lib/konami.js"
	]
	dest = file("${buildDir}/exploded-war/js/app/lib.jquery.min.js")
}

task combineFridgeJs(type: com.eriwen.gradle.js.tasks.CombineJsTask) {
	source = [
		"${projectDir}/src/main/webapp/js/app/util/tools.js",
		"${projectDir}/src/main/webapp/js/app/app.js",
		"${projectDir}/src/main/webapp/js/app/util/jquery-ui-ember.js",
		"${projectDir}/src/main/webapp/js/app/model/postModel.js",
		"${projectDir}/src/main/webapp/js/app/view/postView.js",
		"${projectDir}/src/main/webapp/js/app/controller/fridgeController.js"		
	]
	dest = file("${buildDir}/exploded-war/js/app/fridge.js")
}

task minifyFridgeJs(type: com.eriwen.gradle.js.tasks.MinifyJsTask, dependsOn: 'combineFridgeJs') {
	source = combineFridgeJs
	dest = file("${buildDir}/exploded-war/js/app/fridge.min.js")
	closure {
		closure.compilationLevel = 'SIMPLE_OPTIMIZATIONS'
	}
}

apply plugin: 'css'

css.source {
    app {
        css {
            srcDir "${projectDir}/src/main/webapp/css/app"
            include "*.css"
        }
    }
}

csslint {
	source = css.source.app.css.files
	dest = "${buildDir}/csslint.out"
	options { // Optional
		format = 'lint-xml'
		warnings = ['box-model', 'empty-rules', 'duplicate-properties']
		errors = []
	}
}

task combineFridgeCss(type: com.eriwen.gradle.css.tasks.CombineCssTask) {
	source = css.source.app.css.files
	dest = file("${buildDir}/exploded-war/css/app/fridge.css")
}

task minifyFridgeCss(type: com.eriwen.gradle.css.tasks.MinifyCssTask, dependsOn: 'combineFridgeCss') {
	source = combineFridgeCss
	dest = file("${buildDir}/exploded-war/css/app/fridge.min.css")
}

buildscript {
	repositories {
		add(new org.apache.ivy.plugins.resolver.URLResolver()) {
			name = 'GitHub'
			addArtifactPattern 'http://cloud.github.com/downloads/[organisation]/[module]/[module]-[revision].[ext]'
		}
	}
	dependencies { classpath 'bmuschko:gradle-gae-plugin:0.7.6' }
}

apply plugin: 'gae'

gae {
	httpPort = 8888
	stopKey = null
	disableUpdateCheck = true
	downloadSdk = true
	appcfg { email = 'arnaud.gourlay@gmail.com' }
}

dependencies {
	gaeSdk 'com.google.appengine:appengine-java-sdk:1.7.1'
	compile 'com.google.appengine:appengine-api-1.0-sdk:1.7.1'
	compile 'javax.servlet:servlet-api:2.5'
	compile 'joda-time:joda-time:2.1'
	compile 'com.sun.jersey:jersey-bundle:1.13'
	compile 'com.google.guava:guava:13.0'
	compile 'org.codehaus.jackson:jackson-core-asl:1.9.8'
	compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.8'
	compile 'org.codehaus.jackson:jackson-jaxrs:1.9.8'
	compile 'org.codehaus.jackson:jackson-xc:1.9.8'
	compile 'com.googlecode.objectify:objectify:3.1'
	compile 'javax.persistence:persistence-api:1.0.2'
	compile 'asm:asm:3.3.1'
	testCompile 'junit:junit:4.10'
}